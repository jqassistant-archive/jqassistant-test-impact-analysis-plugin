<jqa:jqassistant-rules xmlns:jqa="http://www.buschmais.com/jqassistant/core/rule/schema/v1.3">

    <concept id="testsuite:CurrentGitBranch">
        <description></description>
        <cypher><![CDATA[
            MATCH
              (repository:Repository)-[:HAS_BRANCH]->(branch:Git:Branch)
            WHERE
              branch.name starts with "heads/"
            MATCH
              (repository)-[:HAS_HEAD]->(head:Git:Commit),
              (branch)-[:HAS_HEAD]->(branchHead:Git:Commit),
               p=shortestPath((branchHead)-[:HAS_PARENT*0..]->(head))
            SET
              branch:Current
            RETURN
              branch.name as CurrentBranch, length(p) as Offset
        ]]></cypher>
    </concept>

    <concept id="testsuite:TestAffectByLastGitCommit">
        <requiresConcept refId="testsuite:CurrentGitBranch" />
        <requiresConcept refId="junit4:TestClass" />
        <description></description>
        <cypher><![CDATA[
            MATCH
              (:Branch:Current)-[:HAS_HEAD]->(:Commit)-[:CONTAINS_CHANGE]->()-[:MODIFIES]->(sourceFile:File)
            WHERE
              sourceFile.relativePath ends with ".java"
            WITH
              sourceFile
            MATCH
              (artifact:Artifact)-[:CONTAINS]->(package:Package)-[:CONTAINS]->(type:Type)
            WHERE
              sourceFile.relativePath ends with (package.fileName + "/" + type.sourceFileName)
            WITH
              artifact, type
            MATCH
              (testPackage:Package)-[:CONTAINS]->(test:Class),
              (test)-[:EXTENDS|IMPLEMENTS*0..]->(:Test:Class)-[:DEPENDS_ON*0..1]->(abstractType:Type),
              (type:Type)-[:EXTENDS|IMPLEMENTS*0..]->(abstractType)<-[:CONTAINS]-(:Artifact)
            WHERE NOT
              (has(test.abstract) and test.abstract)
            SET
              test:Affected
            RETURN
              artifact as Artifact, collect(test) as Tests
            ORDER BY
              Artifact
        ]]></cypher>
    </concept>

    <concept id="testsuite:SurefireSuiteForLastGitCommit">
        <requiresConcept refId="testsuite:TestAffectByLastGitCommit" />
        <description></description>
        <cypher><![CDATA[
            MATCH
              (artifact:Artifact)-[:CONTAINS]->(test:Affected:Test)
            RETURN
              artifact as Artifact, collect(test) as Tests
            ORDER BY
              Artifact
        ]]></cypher>
        <report type="surefire-suite" />
    </concept>
</jqa:jqassistant-rules>
